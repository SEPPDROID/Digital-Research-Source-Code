        .TITLE  COMPARES.  PLM RUNTIME LIBRARY: CMPB/CMPW.
;-----------------------------------------------------------------------
;
;               D I S C L A I M E R   N O T I C E
;               -------------------   -----------
;
;  This document and/or portions of the material and data furnished
;  herewith, was developed under sponsorship of the U. S. Government.
;  Neither the U.S. nor the U.S.D.O.E., nor the Leland Stanford Junior
;  University, nor their employees, nor their respective contractors,
;  subcontractors, or their employees, makes any warranty, express or
;  implied, or assumes any liability or responsibility for accuracy,
;  completeness or usefulness of any information, apparatus, product
;  or process disclosed, or represents that its use will not infringe
;  privately-owned rights.  Mention of any product, its manufacturer,
;  or suppliers shall not, nor is it intended to, imply approval, dis-
;  approval, or fitness for any particular use.  The U. S. and the
;  University at all times retain the right to use and disseminate same
;  for any purpose whatsoever.  Such distribution shall be made by the
;  National Energy Software Center at the Argonne National Laboratory
;  and only subject to the distributee furnishing satisfactory proof
;  that he has a valid license from the Intel Corporation in effect.
;
;-----------------------------------------------------------------------
        .PSECT  PLMRUN_CODE,RD,NOWRT,EXE,GBL,CON,LONG
 
;
;   W = CMPB(SOURCE1,SOURCE2,COUNT)
;
 
SOURCE1=4			; POINTER.
SOURCE2=8			; POINTER.
COUNT=12			; WORD.
 
	.ENTRY	CMPB.,^M<R2,R3>
	CMPC3	COUNT(AP),@SOURCE1(AP),@SOURCE2(AP)
	BNEQ	1$
        DECW    R0              ; STRINGS EQUAL: RETURN 0FFFFH.
	RET
1$:	SUBW3	R0,COUNT(AP),R0	; RETURN INDEX OF FIRST NON-COMPARE.
	RET
 
;
;   W = CMPW(SOURCE1,SOURCE2,COUNT)
;
 
	.ENTRY	CMPW.,^M<R3>
	MOVZWL	COUNT(AP),R0
	BEQL	3$
	MOVL	SOURCE1(AP),R1
	MOVL	SOURCE2(AP),R3
2$:	CMPW	(R1)+,(R3)+
	BNEQ	4$
	SOBGTR	R0,2$
3$:     DECW    R0              ; STRINGS EQUAL: RETURN 0FFFFH.
	RET
4$:	SUBW3	R0,COUNT(AP),R0	; RETURN INDEX OF FIRST NON-COMPARE.
	RET
	.END
