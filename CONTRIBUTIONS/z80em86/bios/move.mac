;**************************************************************************
;*               CP/M 3 BIOS for Z80em86 Z80 CPU emulator                 *
;*                                                                        *
;*                          Bank & Move module                            *
;*                                                                        *
;*                  Copyright (C) 1992-2009 Stewart Kay                   *
;**************************************************************************
;
; ChangeLog (most recent entries are at top)
; ------------------------------------------
; v1.0.0 - 16 February 2009, S.J.Kay
; - Prepare for public release.
;
; v1.00 - 17 April 1995, S.J.Kay
; - Undocumented changes.
;
; v0.00 - 1992, S.J.Kay
; - Initial creation date.

; ?move  : move block of memory
; ?bank  : select bank for code execution
; ?xmove : setup for interbank move

	.z80
;
	maclib	TPORTS.LIB
	maclib	ASMTYPE.LIB
;
	public	?move, ?xmove, ?bank
;
	cseg			;common memory
;
if banked
?move:	out	(bnkmve),a
	add	hl,bc
	ex	de,hl
	add	hl,bc
	ex	de,hl
	ld	bc,0
	ret
;
?bank:	push	af
	out	(bnksel),a
	pop	af
	ret

?xmove:	out	(bnkdta),a
	ret
else
?move:	ex	de,hl
	ldir
	ex	de,hl
?bank:
?xmove:	ret
endif
;
	end
