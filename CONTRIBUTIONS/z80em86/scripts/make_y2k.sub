;===========================================================================
; CP/M 3 - Y2K Build script. SJK, 26 February 2009
;
; This submit file is used to build and create a bootable CP/M floppy disk.
; A HDD bootable disk image can then be created from it.
;
; A copy of all the source directories must be copied to the installed
; location of the emulator.
;
; Place a freshly formatted 1.44MB HD CP/M floppy disk into drive A: before
; starting.
;
; This script may be run in several ways:
; 1) Copy to the HDD on drive C: and execute
;    C:>dostocpm scripts\make_y2k.sub c:
;    C:>submit make_y2k
;
; 2) Under Linux and DosEmu:
;    dosemu -dumb "cd z80em86|z80em86 scripts\make_y2k.sub"
;
; 3) Under DOS:
;    >cd z80em86
;    >z80em86 scripts\make_y2k.sub
;
; Methods 2 and 3 require a current CP/M 3 system that has my 'profile.sub'
; on the HDD disk image.
;
; All Methods require the drive search path to be set to a drive that
; contains the executables. (drive C: is set in 'profile.sub')
;===========================================================================
;
; copy the BIOS source and submit files
dostocpm bios\*.spr bios\*.mac bios\*.asm bios\*.sub bios\*.cpm bios\*.lib bios\*.dat bios\*.rel A: /f

; overwrite the CP/M 3 SPR and REL files with the Y2K ones
dostocpm cpm3_y2k\*.spr cpm3_y2k\*.rel A: /f

; make CP/M 3 and loader system files
A:
submit a:makecpm3.sub

; copy the original CP/M 3 binary files
dostocpm cpm3\*.com cpm3\*.hlp A: /f

; overwrite the CP/M 3 binary files with the Y2K ones
dostocpm cpm3_y2k\*.com cpm3_y2k\*.hlp cpm3_y2k\*.rel cpm3_y2k\*.1st  A: /f

; copy the CP/M 3 support files
dostocpm support\*.com support\*.dat A: /f

; copy selected user files from the 'extras' directory
dostocpm extras\*.com A: /f

; place cpmldr.sys onto the system tracks making the banked build bootable
putldr b A:

; ***** SYSTEM CREATION COMPLETE *****
